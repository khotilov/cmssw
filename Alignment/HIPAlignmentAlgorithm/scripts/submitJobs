#!/bin/tcsh

set iter = $1                          # final iteration number
set odir = `cd $2; pwd`                # get full output path
set name = `basename $odir`            # for job name
set jobs = `ls -d $odir/job*/ | wc -l` # number of jobs

## Submit jobs and iterate

@ i = `cat $odir/main/IOIteration.root` + 1

while (`bjobs -d -w |& grep $name/requeue$i` != "") # check if job name exists
  set name = $name/ # make job name unique to avoid name clash
end

echo "Starting iteration = $i, final iteration = $iter, number of jobs = $jobs\n"

while ($i <= $iter)
  if ($jobs > 0) then
    @ j = 0 # job index

    while ($j < $jobs)
      @ j++

      cp $odir/main/IOIteration.root        $odir/job$j
      cp $odir/main/IOAlignedPositions.root $odir/job$j
    end

    bsub -q cmscaf -C 0 -J $name/align$i\[1-$jobs\] \
         -o $odir/job%I/align$i.out cmsRun $odir/job\$LSB_JOBINDEX/align.cfg

    sleep 5
    set id = `bjobs -J $name/align$i | tail -n1 | awk '{print $1}'`

    bsub -q cmscaf -C 0 -J $name/collect$i -w "done($id)" \
         -o $odir/main/collect$i.out \
         "cmsRun $odir/main/collect.cfg; \
          rm $odir/job*/*.root; \
          gzip $odir/job*/align$i.out; \
          bkill -J $name/requeue$i"

## <collect> job will kill <requeue> job.
## So when <collect> job is done, <requeue> job gets an EXIT status.
## And we can stop requeuing failed <align> jobs.

    while (`bjobs -d -J $name/requeue$i |& grep EXIT` == "")
      bsub -q cmsinter -C 0 -J $name/requeue$i -w "numexit($id, > 0)" \
           -I brequeue -e $id
      sleep 10
    end
  else
    bsub -q cmscaf -C 0 -J $name/collect$i -K \
         -o $odir/main/collect$i.out cmsRun $odir/main/collect.cfg
  endif

  sleep 10
  if (`bjobs -d -J $name/collect$i | tail -n1 | grep EXIT` != "") break
  gzip $odir/main/collect$i.out

  @ i++
end

# hadd $odir/main/histograms.root $odir/job*/histograms.root >& $odir/main/mergeHist
